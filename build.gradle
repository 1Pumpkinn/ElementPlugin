plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'net.rose'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "enginehub-maven"
        url = "https://maven.enginehub.org/repo/"
    }
}

configurations.all {
    resolutionStrategy {
        // Override strict version constraints from WorldEdit
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'com.google.guava' && details.requested.name == 'guava') {
                details.useVersion '33.3.1-jre'
                details.because 'Paper 1.21.10 requires this version'
            }
            if (details.requested.group == 'com.google.code.gson' && details.requested.name == 'gson') {
                details.useVersion '2.11.0'
                details.because 'Paper 1.21.10 requires this version'
            }
            if (details.requested.group == 'it.unimi.dsi' && details.requested.name == 'fastutil') {
                details.useVersion '8.5.15'
                details.because 'Paper 1.21.10 requires this version'
            }
        }
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.9")

    }

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
